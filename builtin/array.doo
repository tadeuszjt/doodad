module array

import builtin/arithmetic
import builtin/tuple
import builtin/convert
import builtin/container
import builtin/builtin
import builtin/compare
import builtin/assert
import builtin/boolean
import builtin/slice


inst{T, S} len{Array{S, T}} (arr&)
    return builtinArrayLen(&arr)


inst{T, S} at{ I64, T, Array{S, T} } (arr&, idx) -> &
    assert(idx >= 0 && idx < arr.len)
    return builtinArrayAt(&arr, idx)


inst{T, S} forEnd{Array{S, T}} (a&)
    return a.len


inst{T, S} forAt{T, Array{S, T}} (a&, idx) -> &
    return a[idx]


inst{S, T} makeSlice{ T, Array{S, T} } (slice)
    let array
    assert(array.len == slice.len)

    for (0, slice.len) -> i
        array[i] = slice[i]

    return array


inst{S, T} slice{Array{S, T}, T} (arr&)
    return builtinSlice(&arr)


inst{S, T} destroy{Array{S, T}} (arr&)
    for (0, arr.len) -> i
        arr[i].destroy
    

inst{S, T} copy{Array{S, T}} (a)
    return a.slice.makeSlice


inst{S, T} compare{Array{S, T}} (a, b)
    return compare(a.slice, b.slice)


inst{S, T} equal{Array{S, T}} (a, b)
    return compare(a, b) == eq()


inst{S, T} add{Array{S, T}} (a, b)
    let c
    for (0, c.len) -> i
        c[i] = a[i] + b[i]
    return c


inst{S, T} subtract{Array{S, T}} (a, b)
    let c
    for (0, c.len) -> i
        c[i] = a[i] - b[i]
    return c


inst{S, T} multiply{Array{S, T}} (a, b)
    let c
    for (0, c.len) -> i
        c[i] = a[i] * b[i]
    return c

