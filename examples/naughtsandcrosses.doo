module naughtsandcrosses

import ../std/io
import ../std/strings
import ../std/rand


type Square { Empty() | Naught() | Cross() }
type Board  [3 [3 Square]]


fn {board Board, io Io} printBoard()
    for board -> row
        io.write('[')
        for row[..] -> r
            switch row[r]
                Empty();  io.write(' ')
                Naught(); io.write('0')
                Cross();  io.write('X')
            if r < (len(row) - 1); io.write(',')
        io.writeLn("]")


fn {board Board} winner() Square
    for board -> row
        let same = true
        for row -> sq
            same = same && sq == row[0]
        if same && row[0] != Empty(); return row[0]

    for board[0][..] -> c
        let same = true
        for board[..] -> r
            same = same && board[r][c] == board[0][c]
        if same && board[0][c] != Empty(); return board[0][c]

    if board[0][0] != Empty()
        let same = true
        for board[..] -> r
            same = same && board[r][r] == board[0][0]
        if same; return board[0][0]
        
    if true
        let same = true
        let end = len(board[0]) - 1
        for board[..] -> r
            same = same && board[r][end - r] == board[0][end]
        if same && board[end][0] != Empty(); return board[end][0]

    return Empty()


fn {board Board} full() bool
    for board -> row
        for row -> sq
            if sq == Empty()
                return false
    return true


fn {board Board, io Io} playerMove() string
    io.writeLn("Make a move")

    data line String
    {io, line}.readLn()
    if line.read(0) -> ('q', _:i64)
        return "quit"

    let (row:i64, n:i64) = line.read(0)
    let (col:i64, n2:i64) = line.read(n)

    let noParse = n == 0 || n2 == 0
    if noParse || !board[..][row] || !board[row][..][col]
        io.writeLn("invalid move")
        return "error"

    if board[row][col] != Empty()
        io.writeLn("board[row][col] is not empty")
        return "error"

    board[row][col] = Naught()
    return "success"


fn {io Io, board Board, rand Rand} cpuMove()
    io.writeLn("cpu moving")
    if board.full()
        return

    data empty [(i64, i64)]
    for board[..] -> r
        for board[r][..] -> c
            if board[r][c] == Empty()
                empty.push((r, c))

    for empty -> (r, c)
        board[r][c] = Cross()
        if board.winner() == Cross()
            return
        board[r][c] = Empty()

    for empty -> (r, c)
        board[r][c] = Naught()
        if board.winner() == Naught()
            board[r][c] = Cross()
            return
        board[r][c] = Empty()

    let (r, c) = empty[rand.I64() % len(empty)]
    board[r][c] = Cross()
    return


fn {board Board, io Io} playerMoveLoop() string
    switch {board, io}.playerMove()
        "quit";    return "quit"
        "success"; return "success"
        "error";   return {board, io}.playerMoveLoop()


fn {io Io} main()
    data board Board
    data rand Rand

    while !board.full() && board.winner() == Empty()
        switch {board, io}.playerMoveLoop()
            "quit"   ; return
            "success";

        {board, io}.printBoard()
        {io, board, rand}.cpuMove()
        {board, io}.printBoard()


