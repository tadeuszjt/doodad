module ox

import builtin/all
import std/io
import std/print
import std/read
import std/option
import std/rand as rond


enum Square {
    empty
    naught
    cross
}
derives Square (store)
derives Square (equal)
derives Square (destroy)


enum Input {
    move(I64, I64)
    quit()
    error()
}
derives Input (builtin::destroy)
derives Input (store::store)


type Board Array{3, Array{3, Square}}
derives Board (destroy)

derives Board (container::len)
derives Board (container::at{I64, Array{3, Square}})
derives Board (forAt{Array{3, Square}})
derives Board (forBegin)
derives Board (forEnd)


inst{P} print{ P, Square } (p&, square)
    switch square
        empty(); p.print('_')
        naught(); p.print('O')
        cross(); p.print('X')

inst{P} print{ P, Board } (p&, board)
    for board -> row
        p.print(row)
        p.print('\n')


fn winner(board Board) Square
    // check for horizontal lines
    for board -> row
        let match = true
        for row -> square
            if square != row[0]
                match = false
        if match && row[0] != empty()
            return row[0]


    // check for vertical lines
    for (0, board[0].len) -> col
        let match = true
        for (0, board.len) -> row
            if board[row][col] != board[0][col]
                match = false

        if match && board[0][col] != empty()
            return board[0][col]


    // check diagonals
    let match = true
    for (0, board.len) -> i | match
        if board[i][i] != board[0][0]
            match = false
    if match && board[0][0] != empty()
        return board[0][0]

    match = true
    for (0, board.len) -> i | match
        let row = board.len - 1 - i
        if board[row][i] != board[board.len - 1][0]
            match = false
    if match && board[board.len - 1][0] != empty()
        return board[board.len - 1][0]

    return empty()


fn getPlayerInput(io &Io) Input
    io.stdout.printLn("Please enter move: <row> <col>")
    
    data line Char.Table

    if io.stdin.getLine(&line) == false
        return quit()

    let row = 0
    let col = 0
    let idx = 0

    if line.read(idx) -> some(n, num)
        row = n
        idx = idx + num
    else
        return error()

    if line.read(idx) -> some(n, num)
        col = n
        idx = idx + num
    else
        return error()

    return move(row, col)


fn cpuMove(io &Io, board &Board)
    io.stdout.printLn("cpu moving ...")

    // find all empty squares
    data empty (I64, I64).Table
    for (0, board.len) -> row
        for (0, board[row].len) -> col
            if board[row][col] == empty()
                empty.push( (row, col) )

    if empty.len == 0
        io.stdout.printLn("cpu can't move")
        return

    // find move that wins
    for empty -> (row, col)
        board[row][col] = cross()
        if winner(board) == cross()
            return
        board[row][col] = empty()

    // find move that opponent wins
    for empty -> (row, col)
        board[row][col] = naught()
        if winner(board) == naught()
            board[row][col] = cross()
            return
        else
            board[row][col] = empty()

    // choose random move
    data rand rond::Rand
    let i = rand.i64(0, empty.len)
    let (row, col) = empty[i]
    board[row][col] = cross()


fn main()
    data io Io
    io.stdout.printLn("naughts and crosses")

    data board Board
    io.stdout.print(board)

    while winner(board).isEmpty
        let moved = false
        while !moved
            switch io.getPlayerInput()
                quit(); return
                error()
                    io.stdout.printLn("parse error")

                move(row, col) | row < 0 || row > (board.len - 1)
                    io.stdout.printLn("row not in range")

                move(row, col) | col < 0 || col > (board.len - 1)
                    io.stdout.printLn("col not in range")

                move(row, col)
                    io.stdout.print("moving: ")
                    io.stdout.print(row)
                    io.stdout.print(", ")
                    io.stdout.printLn(col)
                    board[row][col] = naught()
                    moved = true

                _; io.stdout.print("something")

        io.cpuMove(&board)
        io.stdout.print(board)
//
//    io.stdout.push("winner: ", winner(board), '\n')
//
