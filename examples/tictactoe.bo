module tictactoe

import ../std/io
import ../std/strings
import ../std/rand




type Square { Empty() | Naught() | Cross() }

fn string(sq Square)
    switch sq
        Empty();  return " "
        Naught(); return "0"
        Cross();  return "X"


let board = [
    [Empty(), Empty(), Empty()],
    [Empty(), Empty(), Empty()],
    [Empty(), Empty(), Empty()]
]:[3 [3 Square]]


fn printBoard()
    for [row] board
        print(board[row])
    print()


fn playerMove() bool
    putStrLn("Please enter <row> <column> to move")

    let s = getStrLn()
    if s == "\0"
        return false

    let (row, l) = strings::read(s):(i64, i64)
    if l == 0
        putStrLn("invalid string: " + s)
        return false
    s = s[l..]

    let (col, cl) = strings::read(s):(i64, i64)
    if cl == 0
        putStrLn("invalid string: " + s)
        return false

    if row < 0 || row > len(board) || col < 0 || col > len(board[row])
        putStrLn("invalid move: " + string(row) + " " + string(col))
        return false
    else if board[row][col] != Empty()
        putStrLn("invalid move: " + string(row) + " " + string(col))
        return false

    putStrLn("moving: row " + string(row) + ", col " + string(col))
    board[row][col] = Naught()
    return true


fn cpuMove()
    let cpuWords = ["kssssksk", "beep", "click-click", "ka-chunk", "ding", "bzztzt"]
    let cpuStr = "cpu moving."
    let cpuNumWords = (rand::I64() % 5) + 1
    for [i] | i < cpuNumWords
        cpuStr <- " " <- cpuWords[rand::I64() % len(cpuWords)]
    cpuStr <- "."
    print(cpuStr)

    let emptySquares = []
    for [r] board
        for [c] board[r]
            if board[r][c] == Empty()
                emptySquares <- [(r, c)]
    if len(emptySquares) == 0
        return

    for [i] emptySquares | emptySquares[i] -> (r, c)
        let brd = board
        brd[r][c] = Cross()
        if winner(brd) == Cross()
            board[r][c] = Cross()
            return

    for [i] emptySquares | emptySquares[i] -> (r, c)
        let brd = board
        brd[r][c] = Naught()
        if winner(brd) == Naught()
            board[r][c] = Cross()
            return

    let i = rand::I64() % len(emptySquares)
    let (r, c) = emptySquares[i]
    board[r][c] = Cross()


fn winner(board [3[3 Square]]) Square
    for [r] board
        let same = true
        for [c] board[r]
            if board[r][c] == Empty() || board[r][c] != board[r][0]
                same = false
        if same; return board[r][0]

    for [c] board[0]
        let same = true
        for [r] board
            if board[r][c] == Empty() || board[r][c] != board[0][c]
                same = false
        if same; return board[0][c]

    let same = true
    for [i] | i < len(board) && i < len(board[0])
        if board[i][i] == Empty() || board[i][i] != board[0][0]
            same = false
    if same; return board[0][0]
    
    return Empty()


fn main()
    print("tictactoe")
    printBoard()

    for [i] | i < 10
        let win = winner(board)
        if win != Empty()
            putStrLn("winner is: " + string(win))
            return
        
        while !playerMove();
        cpuMove()
        printBoard()



