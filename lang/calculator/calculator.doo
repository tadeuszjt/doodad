module calculator

import ../../std/io
import ../../std/assert
import ../../std/strings

// Parsing mode: LALR1_BY_SLR(1).
// 
// Grammar:
// 
//      0. $accept -> E
//     -----------------
//      1. E -> E '+' T
//      2.    | E '-' T
//      3.    | T
//      4. T -> T '*' D
//      5.    | D
//      6. D -> D '/' F
//      7.    | F
//      8. F -> NUMBER
//      9.    | '(' E ')'
// 
// LALR1_BY_SLR(1) parsing table:
// 
// ┌────┬─────┬─────┬─────┬─────┬─────┬─────┬────────┬─────┬────┬────┬────┬────┐
// │    │ '+' │ '-' │ '*' │ '/' │ '(' │ ')' │ NUMBER │ $   │ E  │ T  │ D  │ F  │
// ├────┼─────┼─────┼─────┼─────┼─────┼─────┼────────┼─────┼────┼────┼────┼────┤
// │ 0  │     │     │     │     │ s6  │     │ s5     │     │ 1  │ 2  │ 3  │ 4  │
// │ 1  │ s7  │ s8  │     │     │     │     │        │ acc │    │    │    │    │
// │ 2  │ r3  │ r3  │ s10 │     │     │ r3  │        │ r3  │    │    │    │    │
// │ 3  │ r5  │ r5  │ r5  │ s12 │     │ r5  │        │ r5  │    │    │    │    │
// │ 4  │ r7  │ r7  │ r7  │ r7  │     │ r7  │        │ r7  │    │    │    │    │
// │ 5  │ r8  │ r8  │ r8  │ r8  │     │ r8  │        │ r8  │    │    │    │    │
// │ 6  │     │     │     │     │ s6  │     │ s5     │     │ 14 │ 2  │ 3  │ 4  │
// │ 7  │     │     │     │     │ s6  │     │ s5     │     │    │ 9  │ 3  │ 4  │
// │ 8  │     │     │     │     │ s6  │     │ s5     │     │    │ 16 │ 3  │ 4  │
// │ 9  │ r1  │ r1  │ s10 │     │     │ r1  │        │ r1  │    │    │    │    │
// │ 10 │     │     │     │     │ s6  │     │ s5     │     │    │    │ 11 │ 4  │
// │ 11 │ r4  │ r4  │ r4  │ s12 │     │ r4  │        │ r4  │    │    │    │    │
// │ 12 │     │     │     │     │ s6  │     │ s5     │     │    │    │    │ 13 │
// │ 13 │ r6  │ r6  │ r6  │ r6  │     │ r6  │        │ r6  │    │    │    │    │
// │ 14 │ s7  │ s8  │     │     │     │ s15 │        │     │    │    │    │    │
// │ 15 │ r9  │ r9  │ r9  │ r9  │     │ r9  │        │ r9  │    │    │    │    │
// │ 16 │ r2  │ r2  │ s10 │     │     │ r2  │        │ r2  │    │    │    │    │
// └────┴─────┴─────┴─────┴─────┴─────┴─────┴────────┴─────┴────┴────┴────┴────┘


type Cell ( Accept() | Shift i64  | Reduce i64  | Goto i64 | Empty())
type Item ( TokInt i64  | TokSym char  | TokEnd() | State i64  | E_ i64  | E i64  | T i64  | D i64  | F i64)

fn lookupCell(state i64, item Item) Cell
    let array = [
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Goto(1) , Goto(2) , Goto(3) , Goto(4) ]
        [Shift(7) , Shift(8) , Empty()  , Empty()  , Empty() , Empty()  , Empty() , Accept() , Empty() , Empty() , Empty() , Empty() ]
        [Reduce(3), Reduce(3), Shift(10), Empty()  , Empty() , Reduce(3), Empty() , Reduce(3), Empty() , Empty() , Empty() , Empty() ]
        [Reduce(5), Reduce(5), Reduce(5), Shift(12), Empty() , Reduce(5), Empty() , Reduce(5), Empty() , Empty() , Empty() , Empty() ]
        [Reduce(7), Reduce(7), Reduce(7), Reduce(7), Empty() , Reduce(7), Empty() , Reduce(7), Empty() , Empty() , Empty() , Empty() ]
        [Reduce(8), Reduce(8), Reduce(8), Reduce(8), Empty() , Reduce(8), Empty() , Reduce(8), Empty() , Empty() , Empty() , Empty() ]
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Goto(14), Goto(2) , Goto(3) , Goto(4) ]
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Empty() , Goto(9) , Goto(3) , Goto(4) ]
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Empty() , Goto(16), Goto(3) , Goto(4) ]
        [Reduce(1), Reduce(1), Shift(10), Empty()  , Empty() , Reduce(1), Empty() , Reduce(1), Empty() , Empty() , Empty() , Empty() ]
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Empty() , Empty() , Goto(11), Goto(4) ]
        [Reduce(4), Reduce(4), Reduce(4), Shift(12), Empty() , Reduce(4), Empty() , Reduce(4), Empty() , Empty() , Empty() , Empty() ]
        [Empty()  , Empty()  , Empty()  , Empty()  , Shift(6), Empty()  , Shift(5), Empty()  , Empty() , Empty() , Empty() , Goto(13)]
        [Reduce(6), Reduce(6), Reduce(6), Reduce(6), Empty() , Reduce(6), Empty() , Reduce(6), Empty() , Empty() , Empty() , Empty() ]
        [Shift(7) , Shift(8) , Empty()  , Empty()  , Empty() , Shift(15), Empty() , Empty()  , Empty() , Empty() , Empty() , Empty() ]
        [Reduce(9), Reduce(9), Reduce(9), Reduce(9), Empty() , Reduce(9), Empty() , Reduce(9), Empty() , Empty() , Empty() , Empty() ]
        [Reduce(2), Reduce(2), Shift(10), Empty()  , Empty() , Reduce(2), Empty() , Reduce(2), Empty() , Empty() , Empty() , Empty() ]
    ]

    let true = array[..][state]
    let col = 0
    switch item:Item
        TokSym('+'); col = 0
        TokSym('-'); col = 1
        TokSym('*'); col = 2
        TokSym('/'); col = 3
        TokSym('('); col = 4
        TokSym(')'); col = 5
        TokInt(n)  ; col = 6
        TokEnd()   ; col = 7
        E(_)       ; col = 8
        T(_)       ; col = 9
        D(_)       ; col = 10
        F(_)       ; col = 11

    return array[state][col]


fn {stack [Item]} pop() Item
    let item = Item()
    ${ $item = $stack->r0[--$stack->len]; }
    return item


fn {stack [Item]} reduce(production i64)
    fn {stack [Item]} getItem() Item
        let State(_) = stack.pop()
        return stack.pop()

    switch production
        1 // E -> E '+' T
            let T(n1)       = stack.getItem()
            let TokSym('+') = stack.getItem()
            let E(n2)       = stack.getItem()
            stack += [E(n2 + n1)]

        2 // E -> E '-' T
            let T(n1)       = stack.getItem()
            let TokSym('-') = stack.getItem()
            let E(n2)       = stack.getItem()
            stack += [E(n2 - n1)]

        3 // E -> T
            let T(n1)       = stack.getItem()
            stack += [E(n1)]

        4 // T -> T '*' D
            let D(n1)       = stack.getItem()
            let TokSym('*') = stack.getItem()
            let T(n2)       = stack.getItem()
            stack += [T(n2 * n1)]

        5 // T -> D
            let D(n1)       = stack.getItem()
            stack += [T(n1)]

        6 // D -> D '/' F
            let F(n1)       = stack.getItem()
            let TokSym('/') = stack.getItem()
            let D(n2)       = stack.getItem()
            stack += [D(n2 / n1)]

        7 // D -> F
            let F(n1)       = stack.getItem()
            stack += [D(n1)]

        8 // F -> NUMBER
            let TokInt(n)   = stack.getItem()
            stack += [F(n)]

        9 // F -> '(' E ')'
            let TokSym(')') = stack.getItem()
            let E(n)        = stack.getItem()
            let TokSym('(') = stack.getItem()
            stack += [F(n)]


fn {io Io, stack [Item]} printStack()
    for stack -> item:Item
        switch item
            TokSym(c); io.putStrLn("sym: " + string(c))
            TokInt(n); io.putStrLn("int: " + string(n))
            E(n); io.putStrLn("E(" + string(n) + ")")
            F(n); io.putStrLn("F(" + string(n) + ")")
            D(n); io.putStrLn("D(" + string(n) + ")")
            T(n); io.putStrLn("T(" + string(n) + ")")
            State(n); io.putStrLn("state: " + string(n))


fn isRule(item Item) bool
    switch item
        E_(_); return true
        E(_); return true
        T(_); return true
        D(_); return true
        F(_); return true
        _; return false


fn {io Io} main()
    io.putStrLn("calculator4")

    let line = "10 + 2 - 3 - 3"
    data tokens [Item]
    if !tokens.lexString(line)
        assert(false)

    data stack [Item]
    stack += [State(0)]
    let tokenCursor = 0

    for [0..100]
        let token = tokens.at(tokenCursor)
        let item = token
        let state = 0

        // if top of stack is a state, lookup token
        // else get state and item from stack
        switch stack[len(stack) - 1]
            State(s)
                state = s
                item = token
            x | isRule(x)
                item = x
                let State(s) = stack[len(stack) - 2]
                state = s


        switch lookupCell(state, item)
            Shift(state) // Push token onto stack, increase tokenCursor, push next state
                io.putStrLn("-- shifting: " + string(state))
                stack += [tokens[tokenCursor], State(state)]
                tokenCursor = tokenCursor + 1

            Reduce(production) // pop state, replace RHS of production with LHS
                io.putStrLn("-- reducing " + string(production))
                stack.reduce(production)

            Goto(state) // push next state
                io.putStrLn("-- goto " + string(state))
                stack += [State(state)]

            Accept()
                io.putStrLn("-- accept")
                let TokEnd() = token
                return

        {io, stack}.printStack()



type LexResult (LexFail () | LexSuccess(Item, i64))

fn {tokens [Item]} at(idx i64) Item
    if idx == len(tokens); return TokEnd()
    else if idx < len(tokens); return tokens[idx]
    let true = false
    return TokEnd()

fn {s string} at(idx i64) (null | char)
    if idx >= 0 && idx < len(s)
        return conv(s[idx])
    return null

fn lexInt(s string, idx i64) LexResult
    if s.at(idx) -> *char(c) | isDigit(c)
        let n = 0
        while s.at(idx) -> *char(c) | isDigit(c)
            n = n*10 + i64(c - '0')
            idx = idx + 1

        return LexSuccess(TokInt(n), idx)
    return LexFail()

fn lexSym(s string, idx i64) LexResult
    if s.at(idx) -> *char(c) | c == '+' || c == '-' || c == '*' || c == '/' || c == '(' || c == ')'
        return LexSuccess(TokSym(c), idx + 1)
    return LexFail()

fn lex(s string, idx i64) LexResult
    while s.at(idx) -> *char(c) | isSpace(c); idx = idx + 1
    if lexInt(s, idx) -> LexSuccess(a, b); return LexSuccess(a, b)
    if lexSym(s, idx) -> LexSuccess(a, b); return LexSuccess(a, b)
    return LexFail()

fn {tokens [Item]} lexString(s string) bool
    let idx = 0
    while lex(s, idx) -> LexSuccess(token, end)
        idx = end
        tokens += [token]
    return idx == len(s)

