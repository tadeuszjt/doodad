module unordered

import builtin/all
import std/sparse
import std/print


type{T} Key I64
derives{T} Key{T} (copy, destroy)
derives{P, T} Key{T} (print::print{P})


tuple{T} Unordered {
    indices I64.Sparse,
    entries I64.Key.Table,
    values  T.Table
}
derives{T} T.Unordered (copy, destroy)


inst{T} slice::slice{T, T.Unordered} (u&, start, end)
    return u.values.slice(start, end)


inst{T} container::len{T.Unordered } (u&)
    return u.values.len


inst{P, T} print{P, Unordered{T}} (p&, u&)
    p.print(u.values)


fn{T} insertZero(u &T.Unordered) T.Key
    let key = u.indices.sparse::insert(u.values.len)

    u.values.grow
    u.entries.grow
    u.entries[u.entries.len - 1] = key.pretend:Key{I64}

    return key.pretend


fn{T} insert(u &T.Unordered, elem T) T.Key
    let key = u.indices.sparse::insert(u.values.len)

    u.values.grow
    u.entries.grow
    u.values[u.values.len - 1] = elem
    u.entries[u.entries.len - 1] = key.pretend:Key{I64}

    return key.pretend


inst{T} index{ Unordered{T}, T } (u&, idx) -> &
    return u.values[idx]


inst{T} at{ T.Key, T, Unordered{T} } (u&, key) -> &
    let index = u.indices[key.pretend:sparse::Key{I64}]
    return &u.values[index]


fn{T} delete(u &T.Unordered, key T.Key)
    let end    = u.entries.len - 1
    let endKey = u.entries[end]
    let index  = u.indices.at(key.pretend:sparse::Key{I64})

    u.entries[index] = u.entries[end]
    u.values[index] = u.values[end]

    u.indices.at(endKey.pretend:sparse::Key{I64}) = index
    u.indices.sparse::delete(key.pretend)
    u.values.pop
    u.entries.pop


