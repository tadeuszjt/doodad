module print

import builtin/all


func{P, T} print(P, T)


fn{P, A, B} print2(p &P, a A, b B)
    p.print(a)
    p.print(b)


fn{P, A, B, C} print3(p &P, a A, b B, c C)
    p.print(a)
    p.print(b)
    p.print(c)


fn{P, A, B, C, D} print4(p &P, a A, b B, c C, d D)
    p.print(a)
    p.print(b)
    p.print(c)
    p.print(d)


fn{P, A} printLn(p &P, a A)
    p.print(a)
    p.print('\n')


fn{P, A, B} printLn2(p &P, a A, b B)
    p.print3(a, b, '\n')


fn{P, A, B, C} printLn3(p &P, a A, b B, c C)
    p.print3(a, b, c)
    p.print('\n')


inst print{ Char.Table, Char } (t&, c)
    t.push(c)


inst{T} print{ T, Char.Slice } (t&, s)
    for s -> c
        t.print(c)


inst{T} print{ T, I64 } (t&, n)
    if n < 0
        t.print('-')
        n = 0 - n
    else if n == 0
        t.print('0')
        return

    let digits : Char.Table

    while n != 0
        digits.push( '0' + convert(n % 10) )
        n = n / 10

    for (0, digits.len) -> idx
        t.print(digits[digits.len - 1 - idx])


inst{T} print{ T, U8 }  (p&, n); p.print(convert(n) : I64)


inst{T} print{ T, I32 } (p&, n); p.print(convert(n) : I64)


inst{P} print{P, F64} (p&, n)
    let len = 0
    let str : Array{ 32, Char } 
    ${ $len = sprintf( $str.arr, "%g", $n ); }
    assert(len <= str.len)

    for (0, len) -> i
        p.print(str[i])


inst{P} print{P, F32} (p&, n)
    p.print(n.convert : F64)


inst{P} print{P, Bool} (p&, b)
    if b
        p.print("true")
    else
        p.print("false")


inst{T, G} print{ T, G.Table } (t&, g&)
    t.print('[')

    for (0, g.len) -> idx
        t.print(g[idx])
        if idx < (g.len - 1)
            t.print(", ")

    t.print(']')


inst{P, S, T} print{ P, Array{S, T} } (p&, array)
    p.print('[')

    for (0, array.len) -> idx
        p.print(array[idx])
        if idx < (array.len - 1)
            p.print(", ")

    p.print(']')


inst{P, A, B} print{ P, Tuple{A, B} } (a&, b)
    a.print4('(', b.builtinField{0, A, Tuple{A, B}}, ", ", b.1)
    a.print(')')


inst{P, A, B, C} print{ P, Tuple{A, B, C} } (a&, b)
    a.print4('(', b.0, ", ", b.tuplePattern{B, 3, 1, Tuple{A, B, C}})
    a.print3(", ", b.2, ')')


inst{P, A, B, C, D} print{ P, Tuple{A, B, C, D} } (a&, b)
    a.print4('(', b.0, ", ", b.1)
    a.print4(", ", b.2, ", ", b.3)
    a.print(')')
