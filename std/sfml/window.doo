module window

import builtin/all
import std/option
import std/print
import std/sparse
import std/stack
import std/rand

import std/sfml/texture

include <SFML/Graphics.h>
link csfml-graphics
link csfml-window
link csfml-system


tuple Vec2f {
    x F32,
    y F32
}
derives{A, B} Vec2f (convert{Tuple{A, B}})
derives{P}    Vec2f (print::print{P})
derives       Vec2f (copy, destroy, add, subtract, make2{F32, F32}, rand::uniform)


tuple Rectf {
    min Vec2f,
    max Vec2f
}
derives Rectf (make2{Vec2f, Vec2f})



type Mat3f Array{3, Array{3, F32}}
derives Mat3f (copy, destroy, at{I64, Array{3, F32}}, makeSlice{Array{3, F32}})



tuple Colour {
    r U8,
    g U8,
    b U8,
    a U8
}
derives Colour (copy, destroy, make4{U8, U8, U8, U8}, rand::uniform)
derives{P} Colour (print::print{P})

fn white() Colour; return (255, 255, 255, 255)
fn black() Colour; return (0, 0, 0, 255)


tuple Vertex {
    position Vec2f,
    vertColour  Colour,
    texCoord Vec2f
}
derives    Vertex (copy, destroy, make3{Vec2f, Colour, Vec2f})
derives{P} Vertex (print::print{P})


type Quad Array{6, window::Vertex}
derives    Quad (at{ I64, Vertex }, copy, destroy, makeSlice{Vertex})
derives{P} Quad (print::print{P})


tuple Window {
    windowPtr I64
}
inst destroy{Window} (w&)
    let ptr = w.windowPtr
    ${ sfRenderWindow_close((sfRenderWindow*)($ptr)); }



enum Event {
    closed,
    mouseMove(I64, I64),
}
derives Event (copy, destroy)


fn create(w &Window, sizeX I64, sizeY I64, name Char.Slice)
    let ptr = 0
    let nameArray : Char.Table = name.makeSlice

    ${
        char cname[128+1] = {0};
        strncpy(cname, $nameArray.r0, $nameArray.len);
        sfVideoMode mode = {$sizeX, $sizeY, 32};
        sfRenderWindow *window = sfRenderWindow_create(mode, cname, sfClose | sfResize, NULL);
        assert(window != NULL);
        $ptr = (int64_t)(window);
    }
    w.windowPtr = ptr


fn close(w &Window)
    let ptr = w.windowPtr
    ${ sfRenderWindow_close((sfRenderWindow*)($ptr)); }



fn getSize(w &Window) (I64, I64)
    let ptr = w.windowPtr
    let x = 0
    let y = 0
    ${
        sfVector2u vec = sfRenderWindow_getSize((sfRenderWindow*)($ptr));
        $x = vec.x;
        $y = vec.y;
    }
    return (x, y)


fn clearWindow(w &Window, colour Colour)
    let ptr = w.windowPtr
    ${
        sfColor color = {$colour.m0, $colour.m1, $colour.m2, $colour.m3};
        sfRenderWindow_clear((sfRenderWindow*)($ptr), color);
    }


fn display(w &Window)
    let ptr = w.windowPtr
    ${ sfRenderWindow_display((sfRenderWindow*)($ptr)); }


fn isOpen(w &Window) Bool
    let ptr = w.windowPtr
    let isOpen = false
    ${ $isOpen = sfRenderWindow_isOpen((sfRenderWindow*)($ptr)); }
    return isOpen


fn pollEvent(w &Window) Event.Option
    let ptr = w.windowPtr

    let hasEvent = false
    let event:Event

    ${
        sfEvent event;
        bool b = sfRenderWindow_pollEvent((sfRenderWindow*)($ptr), &event);
        if (b) {
            switch (event.type) {
                case sfEvtClosed: {
                    $event.en = 0;
                    $hasEvent = true;
                    break;
                }
                case sfEvtMouseMoved: {
                    $event.en = 1;
                    $event.u1.m0 = event.mouseMove.x;
                    $event.u1.m1 = event.mouseMove.y;
                    $hasEvent = true;
                    break;
                }
                default: {
                    printf("unknown event: %d\n", event.type);
                    $hasEvent = false;
                    break;
                }
            }
        }
    }

    if hasEvent
        return some(event)
    return none()


fn drawQuads(w &Window, quads Slice{Quad}, texture &Texture, transform Mat3f)
    //for (0, quads.len) -> i
    //    let q = quads[i]
    //assert(quads.start == 0)

    let texturePtr = texture.texturePtr
    let ptr = w.windowPtr
    ${
        sfRenderStates states;
        states.texture = NULL; // (sfTexture*)$texturePtr;
        states.blendMode = sfBlendAlpha;
        states.transform = sfTransform_fromMatrix(
            $transform.arr[0].arr[0], $transform.arr[0].arr[1], $transform.arr[0].arr[2],
            $transform.arr[1].arr[0], $transform.arr[1].arr[1], $transform.arr[1].arr[2],
            $transform.arr[2].arr[0], $transform.arr[2].arr[1], $transform.arr[2].arr[2]
        );

        sfRenderWindow_drawPrimitives(
            (sfRenderWindow*)$ptr,
            (const sfVertex *)$quads.ptr,
            $quads.end * 6,
            sfTriangles,
            NULL
            //&states
        );
    }

fn setViewFromRect(w &Window, rect Rectf)
    let ptr = w.windowPtr
    let (l, t, r, b) = (rect.min.x, rect.min.y, rect.max.x, rect.max.y)
    ${
        sfFloatRect rect = {$l, $t, $r, $b};
        sfView *view = sfView_createFromRect(rect);
        sfRenderWindow_setView((sfRenderWindow*)($ptr), view);
    }
